\chapter{Formalizing the topologist's sine curve}

As part of my thesis work, with the help and revision from Prof David Loeffler,
I have formalized a well-known counterexample in topology:
the \textbf{topologist‚Äôs sine curve}.
This classic example illustrates a space that is \textbf{connected}
but not \textbf{path-connected}.
My original proof follows Conrad's paper (\cite{Conrad_connnotpathconn}),
with a few modifications
and some differences from the final
formalization \href{https://leanprover-community.github.io/mathlib4_docs/Counterexamples/TopologistsSineCurve.html}{\textbf{Counterexamples ‚Äì Topologist's Sine Curve}}.
The topologist's sine curve is defined as the graph of $y = \sin(1/x)$
for $x \in (0, \infty)$,
together with the origin $(0, 0)$.
We define three sets in $\mathbb{R}^2$:
\begin{itemize}
  \item $S$: the oscillating curve $\{(x, \sin(1/x)) : x > 0\}$
  \item $Z$: the singleton set $\{(0, 0)\}$
  \item $T$: their union $S \cup Z$
\end{itemize}
In Lean, this is expressed as follows:
\begin{lstlisting}[language=lean]
  open Real Set
  def pos_real := Ioi (0 : ‚Ñù)
  noncomputable def sine_curve := fun x ‚Ü¶ (x, sin (x‚Åª¬π))
  def S : Set (‚Ñù √ó ‚Ñù) := sine_curve '' pos_real
  def Z : Set (‚Ñù √ó ‚Ñù) := { (0, 0) }
  def T : Set (‚Ñù √ó ‚Ñù) := S ‚à™ Z
\end{lstlisting}
We open the \lstinline[language=lean]|Real| and \lstinline[language=lean]|Set| namespaces
to avoid prefixing real number and set operations with \lstinline[language=lean]|Real.|
and \lstinline[language=lean]|Set.|, respectively.
We define the interval $(0, \infty)$ as \lstinline[language=lean]|pos_real|,
using the predefined notation \lstinline[language=lean]|Ioi 0|, from \lstinline[language=lean]|Set|.
The function \lstinline[language=lean]|sine_curve| maps a positive real number
to a point on the topologist's sine curve in $\mathbb{R}^2$.
Here, \lstinline[language=lean]|''| denotes the image of a set under a function.
It's noncomputable because it involves the sine function,
which is not computable in Lean's core logic.
The sets \lstinline[language=lean]|S|, \lstinline[language=lean]|Z|,
and \lstinline[language=lean]|T|
are defined using set operations,
and \lstinline[language=lean]|{ (0, 0) }| denotes the singleton
set containing the point $(0, 0)$.
The sets are subsets of the product space $\mathbb{R}^2$,
represented as \lstinline[language=lean]|‚Ñù √ó ‚Ñù|.
The sin function \lstinline[language=lean]|sin| is defined in the
\lstinline[language=lean]|Real|.

The goal is to prove that $T$ is connected but not path-connected.
Let's start with connectedness.
\section{$T$ is connected}
First of all one can directlly see that  $S$ is connected, since it is the
image of the set ($(0, \infty)$) under the continuous map
$x \mapsto (x, \sin(1/x))$ and a interval in $\mathbb{R}$ is connected.
Moreover, the closure of $S$ is connected, and every set in between a connected
set and its closure are connected.
Since $T$ is contained in the closure of  $S$, $T$ is connected.
This is how a mathematician would argue informally, using known facts.
However, in a formal proof, one must justify each step.
For instance, justifying that $S$ is connected
requires proving that the map
$x \mapsto (x, \sin(1/x))$ is continuous on $(0, \infty)$
and that $(0, \infty)$ is connected.
As we have seen, even showing that a rational number is non-negative
requires several steps and the use of various lemmas from Mathlib.
Similarly, proving that a set is connected can involve multiple steps
for the
newer programmer.
We can use the structure \lstinline[language=lean]|IsConnected|,
to set up the statement and see if we can argue similarly in Lean.
\begin{lstlisting}[language=lean]
lemma S_is_conn : IsConnected S := by sorry
\end{lstlisting}
In the file where \lstinline[language=lean]|IsConnected| is defined,
\texttt{Topology/Connected/Basic.lean}, we see that it requires $S$ to be nonempty and preconnected.
One can verify this by unfolding \lstinline[language=lean]|IsConnected| in the goal.
\begin{lstlisting}[language=lean]
lemma S_is_conn : IsConnected S := by
  unfold IsConnected 
  ‚ä¢ S.Nonempty ‚àß IsPreconnected S
  sorry
\end{lstlisting}
Following the definition of \lstinline[language=lean]|IsPreconnected|, we see that it captures
the usual definition; $S$ cannot be
partitioned into two nonempty disjoint open sets.
This trivially requires
nonemptiness to make sense.
The \lstinline[language=lean]|unfold| tactic helps to expand definitions; one can use it to expand the definition of $S$ or
\lstinline[language=lean]|pos_real| defined before, as well as other Mathlib expressions.
Reflecting our argument, we can check if Mathlib includes the fact
that every interval
is connected and that connectedness is preserved
under continuous maps.
Indeed, in \texttt{Topology/Connected/Interval.lean}, we find the theorem
\lstinline[language=lean]|isConnected_Ioi.image|, stating that the image of an
interval of the form $(a, \infty)$
under a continuous map is connected.

\begin{lstlisting}[language=lean]
lemma S_is_conn : IsConnected S := by
  apply isConnected_Ioi.image
  -- ‚ä¢ ContinuousOn sine_curve (Ioi 0) 
  sorry
\end{lstlisting}
The theorem \lstinline[language=lean]|isConnected_Ioi.image| requires proving the continuity of the map
on the interval $(0, \infty)$, which is expressed as
\lstinline[language=lean]|ContinuousOn sine_curve (Ioi 0)|.
The predicate \lstinline[language=lean]|ContinuousOn f S|
expresses that a function $f$ is continuous on a set $S$, which is what we need to prove now.
The function $x \mapsto (x, \sin(1/x))$ is continuous on $(0, \infty)$ as the
product of two functions continuous on the given domain; the identity map $x \mapsto x$
and the map $x \mapsto \sin(1/x)$.
Here is the full proof in Lean:
\begin{lstlisting}[language=lean]
lemma inv_is_continuous_on_pos_real : ContinuousOn (fun x : ‚Ñù => x‚Åª¬π) (pos_real) := by
  apply ContinuousOn.inv‚ÇÄ
  ¬∑ exact continuous_id.continuousOn
  ¬∑ intro x hx; exact ne_of_gt hx

lemma sin_comp_inv_is_continuous_on_pos_real : ContinuousOn
 (sine_curve) (pos_real) := by
  apply ContinuousOn.prodMk continuous_id.continuousOn
  apply continuous_sin.comp_continuousOn
  exact inv_is_continuous_on_pos_real
\end{lstlisting}
Starting from the bottom lemma, \lstinline[language=lean]|ContinuousOn.prodMk| states that the product of two functions continuous on a set is continuous on that set,
requiring a proof of the continuity of each component.
The first component is the identity map, which is continuous on any set. Mathlib provides
\lstinline[language=lean]|continuous_id.continuousOn| for this purpose.
The second component is the composition of the sine function with the inverse function.
The sine function is continuous everywhere, and for this we can use
\lstinline[language=lean]|continuous_sin|.
The method \lstinline[language=lean]|comp_continuousOn| is accessible from the
fact that \lstinline[language=lean]|continuous_sin| gives
an instance of a continuous map and is generalized
in the \lstinline[language=lean]|ContinuousOn| module.
The theorem \lstinline[language=lean]|Continuous.comp_continuousOn|
states that the composition of a continuous function with a function
that is continuous on a set is continuous on that set,
and requires proof of the continuity
on the set of the inner function.
We separate the proof that the inverse function is continuous on the positive reals
into the auxiliary lemma \lstinline[language=lean]|inv_is_continuous_on_pos_real|.
The theorem \lstinline[language=lean]|continuousOn_inv‚ÇÄ| states that, if a function
is continuous and non-zero on a set, then its inverse is continuous on that set.
The continuity of the identity map is proved as before.
The second argument requires proving that $x \neq 0$ for all $x$ in $(0, \infty)$.
\begin{lstlisting}[language=lean]
  ¬∑ intro x hx
    exact ne_of_gt hx
\end{lstlisting}
The hypothesis \lstinline[language=lean]|hx| states that $x$ is in $(0, \infty)$,
which implies that $x > 0$.
The theorem \lstinline[language=lean]|ne_of_gt| states that if a
real number is greater than zero,
then it is non-zero, which completes the proof.
Thus the final proof goes as follows:
\begin{lstlisting}[language=lean]
lemma S_is_conn : IsConnected S := by
  apply isConnected_Ioi.image 
  ¬∑ exact sin_comp_inv_is_continuous_on_pos_real
\end{lstlisting}

When writing a proof, one starts by working out the informal argument on paper.
Then one tries to translate it into Lean, step by step, looking for theorems in Mathlib.
Afterwards, one can try to optimize the proof by removing unnecessary steps or refactoring it.
Proving properties like continuity and connectedness is very common,
and there are obviously ways to achieve this with less work.
Let's showcase a refactoring of the entire proof.
First, the auxiliary lemmas
can be reduced to one-liners.
\begin{lstlisting}[language=lean]
lemma inv_is_continuous_on_pos_real : ContinuousOn (fun x : ‚Ñù => x‚Åª¬π) (pos_real) :=
  ContinuousOn.inv‚ÇÄ (continuous_id.continuousOn) (fun _ hx =>  ne_of_gt hx)
  
lemma sin_comp_inv_is_continuous_on_pos_real : ContinuousOn
 (sine_curve) (pos_real) :=
 ContinuousOn.prodMk continuous_id.continuousOn <|
  Real.continuous_sin.comp_continuousOn <| (inv_is_continuous_on_pos_real)
\end{lstlisting}
We removed the \lstinline[language=lean]|by| keyword since we can provide a \textbf{term}
that directly proves the statement.
In \lstinline[language=lean]|inv_is_continuous_on_pos_real|, we directly apply
\lstinline[language=lean]|ContinuousOn.inv‚ÇÄ| with the two required arguments.
Notice that we can use a lambda function \lstinline[language=lean]|fun _ hx =>  ne_of_gt hx|
to prove that $x \neq 0$ for all $x$ in $(0, \infty)$
(recall the propositions-as-types correspondence).
In the next lemma, we use
the \lstinline[language=lean]|<\|| reverse application operator,
which allows us to avoid parentheses by changing the order of application.
This means that \lstinline[language=lean]|f <| g <| h| is
equivalent to \lstinline[language=lean]|f (g h)|.
% In our case,
% \lstinline[language=lean]|Real.continuous_sin.comp_continuousOn <\||
% \lstinline[language=lean]|(inv_is_continuous_on_pos_real)|
% is equivalent to
% \lstinline[language=lean]|Real.continuous_sin.comp_continuousOn (inv_is_continuous_on_pos_real)|.
We can inline these two lemmas into the main proof to get a final one-liner:
\begin{lstlisting}[language=lean]
lemma S_is_conn : IsConnected S :=
  isConnected_Ioi.image sine_curve <| continuous_id.continuousOn.prodMk <|
    continuous_sin.comp_continuousOn <|
    ContinuousOn.inv‚ÇÄ continuous_id.continuousOn (fun _ hx => ne_of_gt hx)
\end{lstlisting}
Notice again the use of the \textbf{pipe} operator.
Reading from left to right, we are building up the proof by successive applications:
\begin{itemize}
  \item We start with \lstinline[language=lean]|isConnected_Ioi.image sine_curve|, which states that the image of $(0, \infty)$ under \lstinline[language=lean]|sine_curve| is connected if we can prove the function is continuous.
  \item We then apply \lstinline[language=lean]|continuous_id.continuousOn.prodMk|, which constructs the product of two continuous functions.
  \item Next, \lstinline[language=lean]|continuous_sin.comp_continuousOn| provides the continuity of the sine composition.
  \item Finally, \lstinline[language=lean]|ContinuousOn.inv‚ÇÄ continuous_id.continuousOn (fun _ hx => ne_of_gt hx)| proves the continuity of the inverse function on positive reals.
\end{itemize}
The entire chain can be read as building the continuity proof from the innermost function (the inverse) outward to the complete sine curve function, which is then used to prove that $S$ is connected.
% The expression \lstinline[language=lean]|continuousOn_inv‚ÇÄ.mono fun _ hx ‚Ü¶ hx.ne'|
% applies the theorem and provides the required arguments.
% The \lstinline[language=lean]|mono| method allows us to weaken the domain of
% continuity from \lstinline[language=lean]|x : x = 0 |
% to \lstinline[language=lean]|pos_real|,
% which is a subset of the former.
% The lambda function \lstinline[language=lean]|fun _ hx ‚Ü¶ hx.ne'| proves that $x \neq 0$ for all $x$ in $(0, \infty)$.
% This is a common pattern in Lean, where we often need to prove that certain
% conditions hold for all elements of a set.


% \begin{lstlisting}[language=lean]
% lemma sine_curve_is_continuous_on_pos_real_one_liner : ContinuousOn (sine_curve) (pos_real) :=
%  continuous_id.continuousOn.prodMk <| Real.continuous_sin.comp_continuousOn
%    <| continuousOn_inv‚ÇÄ.mono fun _ hx ‚Ü¶ hx.ne'
% \end{lstlisting}
Since the intersection of $Z$ and $S$ is empty, we cannot
directly conclude that $T$ is connected from the connectedness of its components alone.
However, we can use the fact that every subset between a connected set and its closure is connected.
\begin{theorem}
  Let $C$ be a connected topological space, and denote $\overline{C}$ as its closure.
  It follows that every subset $C \subseteq S \subseteq \overline{C}$ is connected.
\end{theorem}
In Mathlib, this theorem is available as
\lstinline[language=lean]|IsConnected.subset_closure|.
We can set up the statement and progress from there.
\begin{lstlisting}[language=lean]
theorem T_is_conn : IsConnected T := by
  apply IsConnected.subset_closure
  ¬∑ exact S_is_conn -- ‚ä¢ IsConnected ?s
  ¬∑ tauto_set -- ‚ä¢ S ‚äÜ T
  ¬∑ sorry -- ‚ä¢ T ‚äÜ closure S
\end{lstlisting}
The theorem requires three goals:
\begin{itemize}
  \item That $S$ is connected, which was already proved in \lstinline[language=lean]|S_is_conn|.
  \item That $S \subseteq T$, which is a trivial set operation.
        The tactic \lstinline[language=lean]|tauto_set| handles this kind of set tautologies.
  \item That $T \subseteq \overline{S}$ (the closure of $S$), which requires proof.
\end{itemize}
Let's continue with the final point.
\begin{lstlisting}[language=lean]
lemma T_sub_cls_S : T ‚äÜ closure S := by
  intro x hx
  cases hx with
  | inl hxS => exact subset_closure hxS
  | inr hxZ =>
      sorry
\end{lstlisting}
Proving that one set is contained in another can be done naively in a pointwise manner.
We introduce an element $x \in \mathbb{R}^2$ together with the proof that $x \in T$.
Since $T$ is a union, we use \lstinline[language=lean]|cases| to separate the two cases.
When $x \in S$, the goal is trivially solved by \lstinline[language=lean]|exact subset_closure hxS|.
The case where $x \in Z$, requires more work.

Now a trick. Looking for existing theorems using mathlib documentation is
quiet challenging, while you are still learning
the sintax and adapt to the naming convention.
One can use several ways to look for the exact theorems.
A useful tool is Loogle (similar to Haskell's Hoogle),
which helps you find theorems by their type signature or name patterns.
You can access it at \url{https://loogle.lean-lang.org/} or
use it directly in VS Code.
Depending on the previous work in the file,
Lean can already unify the goal with available
theorems and suggest the next step.
For some tactics, adding a question mark causes Lean to automatically search for
the next step involving the use of that tactic.
For instance, one can type
\lstinline[language=lean]|apply?| or
\lstinline[language=lean]|exact?|, which search
for applicable lemmas or definitions
to close the goal.
The tactics \lstinline[language=lean]|rw?|
and \lstinline[language=lean]|simp?|
work similarly but for rewriting and simplification.

At this point, \lstinline[language=lean]|apply?| suggests several ways to proceed,
some involving filters:
\begin{lstlisting}
Try this: refine Frequently.mem_closure ?_
Remaining subgoals:
  ‚ä¢ ‚àÉ·∂† (x : ‚Ñù √ó ‚Ñù) in ùìù x, x ‚àà S
\end{lstlisting}
and others following the more familiar metric space approach:
\begin{lstlisting}
Try this: refine Metric.mem_closure_iff.mpr ?_
Remaining subgoals:
  ‚ä¢ ‚àÄ Œµ > 0, ‚àÉ b ‚àà S, dist x b < Œµ
\end{lstlisting}

The best approach, however, is to think first about how you would tackle the problem
on paper, as mentioned earlier. Since we are working with a metrizable topology on $\mathbb{R}$,
we know that the closure of a set contains all its limit points.
To show that the point $(0, 0)$ is contained in the closure of $S$,
we need to show that it is a limit point of $S$.
Thus, one can define a sequence in $S$ tending to $(0, 0)$,
and the result follows. Instead of using properties of a metric space,
we use filters, as explained before to work with limits.
We can prove that $T \subseteq \overline{S}$, by
showing that the origin is a limit point of $S$.
We construct a sequence $f : \mathbb{N} \to \mathbb{R}^2$ in $S$
converging to $(0,0)$ using
\lstinline[language=lean]|Tendsto|:
\newpage
\begin{lstlisting}[language=lean]
lemma T_sub_cls_S : T ‚äÜ closure S := by
  intro x hx
  cases hx with
  | inl hxS => exact subset_closure hxS
  | inr hxZ =>
      rw [hxZ]
      -- Define sequence: f(n) = (1/(nœÄ), 0)
      let f : ‚Ñï ‚Üí ‚Ñù √ó ‚Ñù := fun n => ((n * Real.pi)‚Åª¬π, 0)
      -- Show f converges to (0, 0)
      have hf : Tendsto f atTop (ùìù (0, 0)) := by
        refine Tendsto.prodMk_nhds ?_ tendsto_const_nhds
        exact tendsto_inv_atTop_zero.comp
          (Tendsto.atTop_mul_const' Real.pi_pos tendsto_natCast_atTop_atTop)
      -- Show f eventually takes values in S
      have hf' : ‚àÄ·∂† n in atTop, f n ‚àà S := by
        filter_upwards [eventually_gt_atTop 0] with n hn
        exact ‚ü®(n * Real.pi)‚Åª¬π,
          inv_pos.mpr (mul_pos (Nat.cast_pos.mpr hn) Real.pi_pos),
          by simp [f, sine_curve, inv_inv, Real.sin_nat_mul_pi]‚ü©
      -- Apply sequential characterization of closure
      exact mem_closure_of_tendsto hf hf'
\end{lstlisting}
The proof is already reduced as much as possible.
Let's break down what's happening in without getting into details.
Using \lstinline[language=lean]|let|, we define
$f(n) = \left(\frac{1}{n\pi}, 0\right)$,
which we will show converges to $(0,0)$
and stays in $S$.

\begin{itemize}

  \item \textbf{Convergence proof} (\lstinline[language=lean]|hf|):
        We show \lstinline[language=lean]|Tendsto f atTop (ùìù (0, 0))|.
        With \lstinline[language=lean]|Tendsto.prodMk_nhds|,
        we need to show that both coordinates tend to $0$, separately.
        For the first coordinate, we compose
        \lstinline[language=lean]|tendsto_inv_atTop_zero|
        (which states $\frac{1}{x} \to 0$ as $x \to \infty$) with the
        fact that $n\pi \to \infty$.
        The second constant coordinate is handled by
        \lstinline[language=lean]|tendsto_const_nhds|
  \item \textbf{Membership proof} (\lstinline[language=lean]|hf'|):
        We show \lstinline[language=lean]|‚àÄ·∂† n in atTop, f n ‚àà S|, meaning $f(n) \in S$
        for all sufficiently large $n$.
        We use \lstinline[language=lean]|filter_upwards|,
        which allows us to combine
        hypotheses about properties that hold eventually to prove another property holds eventually.
        Here, we combine it with \lstinline[language=lean]|eventually_gt_atTop 0|,
        which states that eventually $n > 0$.
        For such $n$, we show $f(n) = \left(\frac{1}{n\pi}, 0\right)$ is in $S$ by noting that
        the second term is:
        $$
          \sin\left(\frac{1}{\left(\frac{1}{n\pi}\right)}\right) = \sin(n\pi) = 0.
        $$

\end{itemize}
Finally, \lstinline[language=lean]|mem_closure_of_tendsto|
combines these facts:
if a sequence eventually stays in $S$ and converges to $x$,
then $x$ is in the closure of $S$.
% (ALTERNATIVE PROOF)

% \begin{proof}
%   To show that $S$ lies in the closure of $S^+$, we have to express each $p \in S$ as a limit of a
%   sequence of points in $S^+$. If $p \in S^+$, we use the constant sequence $\{p, p, \ldots\}$. If $p = (0, y)$ with
%   $|y| \leq 1$, we argue as follows. Certainly $y = \sin(\theta)$ for some $\theta \in [-\pi, \pi]$, whence $y = \sin(\theta + 2n\pi)$
%   for all positive integers $n$. Thus, for $x_n = 1/(\theta + 2n\pi) > 0$ we have $\sin(1/x_n) = y$ for all $n$. Since
%   $x_n \to 0$ as $n \to \infty$, we have $(x_n, \sin(1/x_n)) = (x_n, y) \to (0, y)$.
% \end{proof}
\subsubsection{Finalising the first part of the proof}
If you are a one-liner enthusiast like me, you don't mind trying to combine
bits and pieces to get a clean final result.
We can simplify the final theorem as follows initially:
\begin{lstlisting}[language=lean]
theorem T_is_conn : IsConnected T := 
  IsConnected.subset_closure S_is_conn (by tauto_set) T_sub_cls_S
\end{lstlisting}
The second argument is still in tactic mode with \lstinline[language=lean]|by tauto_set|,
but it looks clean and we can keep it as is.
With a bit of courage, we can also inline the proof of \lstinline[language=lean]|S_is_conn|
(while \lstinline[language=lean]|T_sub_cls_S| is way too long to inline)
to get a more self-contained one-liner:
\begin{lstlisting}[language=lean]
theorem T_is_conn : IsConnected T :=
  IsConnected.subset_closure (isConnected_Ioi.image sine_curve <|
    continuous_id.continuousOn.prodMk <|
    Real.continuous_sin.comp_continuousOn <|
    ContinuousOn.inv‚ÇÄ continuous_id.continuousOn
    (fun _ hx => ne_of_gt hx)) (by tauto_set) T_sub_cls_S
\end{lstlisting}
Making these amendments is not only for the sake of shortening the proof.
Lean will, obviously, compile the proof faster by not
entering tactic mode or using multiple tactics.
Tactics internally hide many operations they automatically perform to close the goal.
Moreover, if we directly provide
a term for the proof, Lean will infer and unify everything by definitional
equality.
By providing explicit proof terms, we give Lean less work to do, making the
proof more transparent and efficient.
This practice of "golfing" is essential in a huge library such as Mathlib
community that needs to balance performance and maintainability.
From now on the rest of th code will be presented in it's reduced form.
Here is the link to the entire first part of the proof:
[\href{https://live.lean-lang.org//#codez=JYWwDg9gTgLgBAWQIYwBYBtgCMBQEwCmAdnACr4ToQDmAnnAGLDowFRwDKB8A8lACZscOQQDM4kAM4B9KASTo4ALgC8nbgDoAkhGBwAFAAZlcQLiEAShxEIRAMYRwAVxhIs6AnDFxJwIgWm2jlAAbh6qcKKOJAAecIBlhAbRADRwAEry6Bo+JPrRgN4EAJ3mll4cJlzw+qZwAOtm5spq+tn+gSEEDQDknRIQMnIKIgTiAFrl3AbVdRaNcADeBoYphg0AvkPipOOVU/WzZYBURHAjwu4gIEhk0pKOWAHoMmVKZHCAYURwtlQ3cpyzWLQ4OBwXwwKAQOCxVDRQEfJCSAiSOBQuAAd2AaBhAB9gURFFCyioAHxwAjRJC2eA3LDwmD3PpBDz4rE49hQsZEmFAoFQFFwADabIAupyue54OJnoBUQjggCTCMy1eXhSIkEhEgz6EgAKjSGQ0YGA5kKy0sXK5qCQoSRErIxH4khg4PEKHIYAMgFYNwC7O4tjQ1wv8Raa5KJfB49WD+AgANbSIioO1wAD80jgrCIdodARs9pjcckAa5pPJ8FT6Yg0l8wWkzvw0gAXmwIBp7OB86b9EwWGwNKRbfbG9WwNIQI50JmiPaeukFHrgNIpCnexmiCgAMJw2kDqswF0m01Ii2M0Q9Z6AACJAAW4cBIvjgA5S4hIgAgiX5+gF7rnBztQaSOMAopACRE+QIUIiBgRwFHQWhpGoDdt3wOBDEFVF0VQS8kSIVsSTJCk4EAC/INTgbUp0yfVDQKJJMKBCs5z6DRwHYfRh1Hed9AAORQJt1xoyQ6LAVkiAaYiZ248wKLfU1/m8UBXT5UQUhaAIglCFJqIrFIhOyGMUCHEc52AQVAEvyfNCxwkACBAOlvn8CBRGkEs+ytK1OmENACGgcyrmAGR7CIEhni0SQVxsPwKQIfgXlUGEAqC3yCFC/gsluGlLIZAwvJikLWH4aQdGADRQCQagPAUtpLQAHkxEUfJgXxHAgRwZGABLqtq+rJB4IgwwgCNIzgCqRSElqiDqhrrl8JsHEHIaRvakh+tNGKauGtqOvyohgkAAIIPhsJaZvLZqdtahqOpFfRlTgZNkTVPxpBs6D4ChYoDEklxnDLGkGlIa5bnuR4gA}{link to Lean live}]
\begin{note}
  The proof merged into the Mathlib library,
  takes $Z$ as $\{0\} \times [-1,1]$
  instead of the singleton $\{(0,0)\}$.
  This, together with the fact that $T$ equals the closure of $S$,
  yields a stronger and more general result.
  This stronger version shows that a closed set
  (specifically, the closure of $S$) can be connected but not path-connected.
  Showing that forming closure can destroy
  the property of path connectedness for subsets of a topological space.
\end{note}
\section{$T$ is not path-connected}
The main and most substantial part is showing that $T$ is not path-connected.
Showing this informally already requires constructing and
pointing out various steps in order to convince
an ideal reader.
One can argue informally by contradiction.
Suppose a path exists in the topologist's sine curve $T$
connecting a point in $S$ to a point in $Z$.
As the path approaches the $y$-axis (where $x \to 0$),
the $y$-coordinate must oscillate infinitely between $-1$ and $1$
due to the behavior of $\sin(1/x)$ as $x \to 0^+$.
This infinite oscillation contradicts the continuity of the path,
which is a fundamental requirement for path-connectedness.
To be more precise, we need to construct a sequence
that it eventually oscillates, establishing the contradiction.
We start by setting up the theorem:
\begin{lstlisting}[language=lean]
theorem T_is_not_path_conn : ¬¨ (IsPathConnected T) := 
  by sorry
\end{lstlisting}
In mathematics, we normally define a path-connected space as follows.
\begin{definition}
  A topological space $X$ is said to be path-connected if for every two points $a, b \in X$, there
  exists a path, i.e., a continuous map $p : [0, 1] \to X$ such that $p(0) = a$ and $p(1) = b$.
\end{definition}
The interval $[0, 1]$ is the standard choice for the domain of paths.
% In Mathlib, \lstinline[language=lean]|PathConnectedSpace X| is a type class that asserts the entire
% topological space $X$ is path-connected, while
\lstinline[language=lean]|IsPathConnected S| is a predicate used to infer that a subset $S$
of a topological space is path-connected.
\begin{lstlisting}[language=lean]
def IsPathConnected (F : Set X) : Prop :=
  ‚àÉ x ‚àà F, ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà F ‚Üí JoinedIn F x y
\end{lstlisting}
The auxiliary predicate \lstinline[language=lean]|JoinedIn| is defined as:
\begin{lstlisting}[language=lean]
def JoinedIn (S : Set X) (x y : X) : Prop :=
  ‚àÉ Œ≥ : Path x y, ‚àÄ t, Œ≥ t ‚àà S
\end{lstlisting}
where \lstinline[language=lean]|Path x y| denotes a continuous map $\gamma : [0,1] \to X$ with
$\gamma(0) = x$ and $\gamma(1) = y$.
We can use the \lstinline[language=lean]|unitInterval| \textbf{subtype} of
\lstinline[language=lean]|‚Ñù| representing the interval
$[0,1]$.
Now let's start with the first part of the proof:
\begin{lstlisting}[language=lean]
theorem T_is_not_path_conn : ¬¨ (IsPathConnected T) := by
  -- Assume we have a path from z = (0, 0) to w = (1, sin(1))
  have hz : z ‚àà T := Or.inr rfl
  have hw : w ‚àà T := Or.inl ‚ü®1, ‚ü®zero_lt_one' ‚Ñù, rfl‚ü©‚ü©
  intro p_conn
  apply IsPathConnected.joinedIn at p_conn
  specialize p_conn z hz w hw
  let p := JoinedIn.somePath p_conn
\end{lstlisting}
We introduce two points: $z = (0, 0)$ and $w = (1, \sin(1))$, and prove they are both in $T$, in
\lstinline[language=lean]|hz hw| and \lstinline[language=lean]|hw| (we use the introduction rule
for Or, seen it as a sum type).
Using \lstinline[language=lean]|intro p_conn|, we assume that $T$ is path-connected.
Notice that the goal is now \lstinline[language=lean]|False|, meaning we must find a contradiction.
The last three lines extract an explicit path \lstinline[language=lean]|p| connecting $z$ and $w$.
\lstinline[language=lean]|apply IsPathConnected.joinedIn at p_conn|
transforms the path-connectedness assumption into the statement
that any two points in $T$ are joined.
\lstinline[language=lean]|specialize p_conn z hz w hw| specializes this
to our specific points $z$ and $w$.
Then we extrat a path and store it using
\lstinline[language=lean]|let p := JoinedIn.somePath p_conn|.

Conrad's paper (\cite{Conrad_connnotpathconn}) defines a time $t_0 \in [0, 1]$
as the first time the path $p$ jumps from $(0,0)$ to the graph of $\sin(1/x)$, where
the x-coordinate map ($x: \mathbb{R}^2 \to \mathbb{R} $) of $p$ is positive.
$$
  t_0 = \inf\{t \in [0, 1] : x(p(t)) > 0\}
$$
The argument then uses the continuity of the $x$-coordinate map composed with the path $p$.
By continuity at $t_0$, we can find a neighborhood around $t_0$
where the path stays close to $(0,0)$. Specifically, with $\varepsilon = 1/2$,
there exists $\delta > 0$ such that for all $t$ with $|t - t_0| < \delta$,
we have $\|p(t) - p(t_0)\| < 1/2$.
We want to show the oscillating behavior around (0,0) indeed.
To simplify some steps, we instead define
$$
  t_0 = \sup\{t \in [0, 1] : x(p(t)) = 0\}
$$
to be the last time the path remains at $(0,0)$.
The same continuity argument applies with this definition.
\begin{lstlisting}[language=lean]
-- Consider the composition of the x-coordinate map with p, which is continuous
have xcoord_pathcont : Continuous fun t ‚Ü¶ (p t).1 := continuous_fst.comp p.continuous
-- Let t‚ÇÄ be the last time the path is on the y-axis
let t‚ÇÄ : unitInterval := sSup {t | (p t).1 = 0}
let xcoord_path := fun t => (p t).1
-- The x-coordinate of the path at t‚ÇÄ is 0
have hpt‚ÇÄ_x : (p t‚ÇÄ).1 = 0 :=
  (isClosed_singleton.preimage xcoord_pathcont).sSup_mem ‚ü®0, by aesop‚ü©
-- By continuity of the path, we can find a Œ¥ > 0 such that
-- for all t in [t‚ÇÄ - Œ¥, t‚ÇÄ + Œ¥], ||p(t) - p(t‚ÇÄ)|| < 1/2
-- Hence the path stays in a ball of radius 1/2 around (0, 0)
obtain ‚ü®Œ¥, hŒ¥, ht‚ü© : ‚àÉ Œ¥ > 0, ‚àÄ t, dist t t‚ÇÄ < Œ¥ ‚Üí
  dist (p t) (p t‚ÇÄ) < 1/2 :=
  Metric.eventually_nhds_iff.mp <| Metric.tendsto_nhds.mp (p.continuousAt t‚ÇÄ) _ one_half_pos
\end{lstlisting}
The final statement uses the \lstinline[language=lean]|obtain| tactic to extract witnesses from an existential statement.
This tactic destructures the existential quantifier $\exists \delta > 0, \ldots$ into
\lstinline[language=lean]|Œ¥| (the distance), \lstinline[language=lean]|hŒ¥| (the proof that $\delta > 0$),
and \lstinline[language=lean]|ht| (the proof that the distance condition holds).
Since $\mathbb{R}^2$ is a metric space, we can work with the distance function \lstinline[language=lean]|dist : ‚Ñù √ó ‚Ñù ‚Üí ‚Ñù √ó ‚Ñù ‚Üí ‚Ñù|,
which computes the Euclidean distance between two points.
The statement \lstinline[language=lean]|dist t t‚ÇÄ < Œ¥| expresses $|t - t_0| < \delta$ in the unit interval,
while \lstinline[language=lean]|dist (p t) (p t‚ÇÄ) < 1/2| expresses $\|p(t) - p(t_0)\| < 1/2$ in $\mathbb{R}^2$.
We assert that the path $p$ is
continuous at $t_0$ by \lstinline[language=lean]|p.continuousAt t‚ÇÄ|.
\lstinline[language=lean]|Metric.tendsto_nhds.mp| converts this
to the metric space characterization:
for any $\varepsilon > 0$, there exists $\delta > 0$ such that
points within $\delta$ of $t_0$ map to points within $\varepsilon$ of $p(t_0)$.
While, \lstinline[language=lean]|Metric.eventually_nhds_iff.mp| further unpacks
this into the $\forall t, dist\ t\ t_0 < \delta \to dist\ (p\ t)\ (p\ t_0) < \varepsilon$
form, requiring positivity of $\varepsilon = 1/2$ (\lstinline[language=lean]|one_half_pos|).

We can find a time $t_1$ greater than $t_0$ that remains in the neighborhood of $t_0$,
and obtain a point $a = x(p(t_1))) > 0$ which is positive.
\begin{lstlisting}[language=lean]
-- Let t‚ÇÅ be a time when the path is not on the y-axis
-- t‚ÇÅ is in (t‚ÇÄ, t‚ÇÄ + Œ¥], hence t‚ÇÅ > t‚ÇÄ
obtain ‚ü®t‚ÇÅ, ht‚ÇÅ‚ü© : ‚àÉ t‚ÇÅ, t‚ÇÅ > t‚ÇÄ ‚àß dist t‚ÇÄ t‚ÇÅ < Œ¥ := by
  let s‚ÇÄ := (t‚ÇÄ : ‚Ñù) -- cast t‚ÇÄ from unitInterval to ‚Ñù for manipulation
  let s‚ÇÅ := min (s‚ÇÄ + Œ¥/2) 1
  have hs‚ÇÄ_delta_pos : 0 ‚â§ s‚ÇÄ + Œ¥/2 := add_nonneg t‚ÇÄ.2.1 (by positivity)
  have hs‚ÇÅ : 0 ‚â§ s‚ÇÅ := le_min hs‚ÇÄ_delta_pos zero_le_one
  have hs‚ÇÅ': s‚ÇÅ ‚â§ 1 := min_le_right ..
  sorry
-- Let a = xcoord_path t‚ÇÅ > 0
-- This follows from the definition of t‚ÇÄ and t‚ÇÄ < t‚ÇÅ
-- so t‚ÇÅ must be in S, which has positive x-coordinate
let a := (p t‚ÇÅ).1
have ha : a > 0 := by
  obtain ‚ü®x, hxI, hx_eq‚ü© : p t‚ÇÅ ‚àà S := by
    cases p_conn.somePath_mem t‚ÇÅ with
    | inl hS => exact hS
    | inr hZ =>
      -- If p t‚ÇÅ ‚àà Z, then (p t‚ÇÅ).1 = 0
      have : (p t‚ÇÅ).1 = 0 := by rw [hZ]
      -- So t‚ÇÅ ‚â§ t‚ÇÄ, contradicting t‚ÇÅ > t‚ÇÄ
      have hle : t‚ÇÅ ‚â§ t‚ÇÄ := le_sSup this
      have hle_real : (t‚ÇÅ : ‚Ñù) ‚â§ (t‚ÇÄ : ‚Ñù) := Subtype.coe_le_coe.mpr hle
      have hgt_real : (t‚ÇÅ : ‚Ñù) > (t‚ÇÄ : ‚Ñù) := Subtype.coe_lt_coe.mpr ht‚ÇÅ.1
      linarith
  simpa only [a, ‚Üê hx_eq] using hxI
\end{lstlisting}
The code is quite convoluted, and i will omit a
detailed explanation as well as some part of it.
However, it's worth mentioning a few key technical points.
The type \lstinline[language=lean]|unitInterval| is a \textbf{subtype}
of $\mathbb{R}$,
defined as $\{x : \mathbb{R} \mid 0 \leq x \leq 1\}$.
In Lean, a subtype \lstinline[language=lean]|{x : Œ± // P x}|
bundles a value $x$ of type $\alpha$
together with a proof that $x$ satisfies the predicate $P$,
similar as for  \lstinline[language=lean]|Set|.
Anyway, rather than
being \lstinline[language=lean]|Set| (as subsets), they are type itself.
In particular, their terms do not share the type
of the underlying \lstinline[language=lean]|Set|.
Consequently, they lack of arithmetic properties
of the real numbers for instance.
We need to cast them to $\mathbb{R}$
(with \lstinline[language=lean]|let s‚ÇÄ := (t‚ÇÄ : ‚Ñù)|)
first, then cast back to \lstinline[language=lean]|unitInterval|
by providing proofs that the bounds $[0, 1]$ are satisfied (\lstinline[language=lean]|hs‚ÇÅ|, \lstinline[language=lean]|hs‚ÇÅ'|).
In the second case of the inner statment of have \lstinline[language=lean]|ha : a > 0|,
if $p(t_1) \in Z$, then $(p\ t_1).1 = 0$ by definition of $Z = \{(0,0)\}$.
This implies $t_1 \leq t_0$ by the definition of $t_0$ as the supremum.
However, we also have $t_1 > t_0$ from our construction of $t_1$ (\lstinline[language=lean]|ht‚ÇÅ.1|).
The tactic \lstinline[language=lean]|linarith|, an automated solver for linear arithmetic,
recognizes this contradiction by observing both
\lstinline[language=lean]|hle_real : (t‚ÇÅ : ‚Ñù) ‚â§ (t‚ÇÄ : ‚Ñù)| and
\lstinline[language=lean]|hgt_real : (t‚ÇÅ : ‚Ñù) > (t‚ÇÄ : ‚Ñù)|.
Since these statements are contradictory, \lstinline[language=lean]|linarith|
proves \lstinline[language=lean]|False|.
Lemmas like \lstinline[language=lean]|Subtype.coe_lt_coe|
allow us to transfer inequalities between the subtype and its underlying type,
needed for \lstinline[language=lean]|linarith|.

Finally, \lstinline[language=lean]|simpa only [a, ‚Üê hx_eq] using hxI| completes the proof.
The tactic \lstinline[language=lean]|simpa| combines simplification (\lstinline[language=lean]|simp|)
with assumption matching. The directive \lstinline[language=lean]|only [a, ‚Üê hx_eq]|
unfolds the definition of $a = (p\ t_1).1$ and rewrites using \lstinline[language=lean]|hx_eq|
in the reverse direction, transforming the goal from \lstinline[language=lean]|(p t‚ÇÅ).1 > 0|
to \lstinline[language=lean]|(sine_curve x).1 > 0|.
Since \lstinline[language=lean]|sine_curve x = (x, sin(1/x))|, this simplifies to \lstinline[language=lean]|x > 0|,
which is exactly the hypothesis \lstinline[language=lean]|hxI|.
The \lstinline[language=lean]|using hxI| clause, applies this hypothesis to close the goal.

Next, the image $x(p([t_0, t_1]))$ is connected (as the continuous image of a connected set),
and it contains $0 = x(p(t_0))$ and $a = x(p(t_1))$.
Since every connected subset of $\mathbb{R}$ is an interval, we have
$$
  [0, a] \subseteq x(p([t_0, t_1]))
$$
This will be crucial for the next step, where we show that the path must oscillate.
\begin{lstlisting}[language=lean]
  -- The image x(p([t‚ÇÄ, t‚ÇÅ])) is connected and contains 0 and a
  -- Therefore [0, a] ‚äÜ x(p([t‚ÇÄ, t‚ÇÅ]))
  have Icc_of_a_b_sub_Icc_t‚ÇÄ_t‚ÇÅ: Set.Icc 0 a ‚äÜ xcoord_path '' Set.Icc t‚ÇÄ t‚ÇÅ :=
     IsConnected.Icc_subset
      ((isConnected_Icc (le_of_lt ht‚ÇÅ.1)).image _ xcoord_pathcont.continuousOn)
      (‚ü®t‚ÇÄ, left_mem_Icc.mpr (le_of_lt ht‚ÇÅ.1), hpt‚ÇÄ_x‚ü©)
      (‚ü®t‚ÇÅ, right_mem_Icc.mpr (le_of_lt ht‚ÇÅ.1), rfl‚ü©)
\end{lstlisting}
Now we construct a sequence that demonstrates the contradiction.
Recall that $\sin(\theta) = 1$ if and only if $\theta = \frac{(4k + 1)\pi}{2}$ for some $k \in \mathbb{Z}$.
Therefore, $(x, \sin(1/x)) = (x, 1)$ when
$$
  x = \frac{2}{(4k + 1)\pi}
$$
for $k \in \mathbb{N}$. As $k \to \infty$, these $x$-values approach 0,
so infinitely many of them lie in any interval $[0, a]$.
We define this sequence and establish its key properties:
\begin{lstlisting}[language=lean]
noncomputable def xs_pos_peak := fun (k : ‚Ñï) => 2/((4 * k + 1) * Real.pi)
lemma xs_pos_peak_tendsto_zero : Tendsto xs_pos_peak atTop (ùìù 0) := sorry
lemma xs_pos_peak_nonneg : ‚àÄ k : ‚Ñï, 0 ‚â§ xs_pos_peak k := sorry
lemma sin_xs_pos_peak_eq_one (k : ‚Ñï) : Real.sin ((xs_pos_peak k)‚Åª¬π) = 1 := sorry
\end{lstlisting}
The crucial property is that this sequence eventually enters $[0, a]$:
\begin{lstlisting}[language=lean]
-- For any k ‚àà ‚Ñï, sin(1/xs_pos_peak(k)) = 1
-- Since xs_pos_peak converges to 0 as k ‚Üí ‚àû,
-- there exist indices i ‚â• 1 for which xs_pos_peak i ‚àà [0, a]
have xpos_has_terms_in_Icc_of_a_b : ‚àÉ i : ‚Ñï, i ‚â• 1 ‚àß xs_pos_peak i ‚àà Set.Icc 0 a := sorry
\end{lstlisting}
This gives us points on the topologist's sine curve with $y$-coordinate equal to $1$,
lying arbitrarily close to the $y$-axis.

Now we can establish the final contradiction.
Since $[0, a] \subseteq x(p([t_0, t_1]))$ by the previous argument,
and $\text{xs\_pos\_peak}(i) \in [0, a]$ for some $i$,
there must exist some $t' \in [t_0, t_1]$ such that $x(p(t')) = \text{xs\_pos\_peak}(i)$.
This means $p(t') = (\text{xs\_pos\_peak}(i), \sin(1/\text{xs\_pos\_peak}(i))) = (\text{xs\_pos\_peak}(i), 1)$,
so the $y$-coordinate of $p(t')$ equals $1$.
However, since $t' \in [t_0, t_1] \subseteq [t_0, t_0 + \delta)$,
we have $\text{dist}(t', t_0) < \delta$, which by our earlier continuity argument implies
$\|p(t') - p(t_0)\| < 1/2$.
But $\|p(t') - (0,0)\| \geq |(p(t')).2| = |1| = 1 > 1/2$,
yielding a contradiction.
\begin{lstlisting}[language=lean]
-- Show there exists time t' in [t‚ÇÄ, t‚ÇÅ] ‚äÜ [t‚ÇÄ, t‚ÇÄ + Œ¥) such that p(t') = (*, 1)
obtain ‚ü®t', ht', hpath_t'‚ü© : ‚àÉ t' ‚àà Set.Icc t‚ÇÄ t‚ÇÅ, (p t').2 = 1 := sorry
-- Derive the final contradiction using t', ht', hpath_t'
-- First show that p t‚ÇÄ = (0, 0)
have hpt‚ÇÄ : p t‚ÇÄ = (0, 0) := sorry
-- t' is within Œ¥ of t‚ÇÄ (since t' ‚àà [t‚ÇÄ, t‚ÇÅ] and dist t‚ÇÄ t‚ÇÅ < Œ¥)
have t'_close : dist t' t‚ÇÄ < Œ¥ := by
  calc dist t' t‚ÇÄ
      ‚â§ dist t‚ÇÅ t‚ÇÄ := dist_right_le_of_mem_uIcc (Icc_subset_uIcc' ht')
    _ = dist t‚ÇÄ t‚ÇÅ := dist_comm _ _
    _ < Œ¥ := ht‚ÇÅ.2
-- By continuity, p(t') should be close to p(t‚ÇÄ)
have close : dist (p t') (p t‚ÇÄ) < 1/2 := ht t' t'_close
-- But p(t') has y-coordinate 1, so it's actually far from p(t‚ÇÄ) = (0, 0)
have far : 1 ‚â§ dist (p t') (p t‚ÇÄ) := by
  calc 1 = |(p t').2 - (p t‚ÇÄ).2| := by simp [hpath_t', hpt‚ÇÄ]
      _ ‚â§ ‚Äñp t' - p t‚ÇÄ‚Äñ := norm_ge_abs_snd
      _ = dist (p t') (p t‚ÇÄ) := by rw [dist_eq_norm]
-- This is a contradiction: 1 ‚â§ dist (p t') (p t‚ÇÄ) < 1/2
linarith
\end{lstlisting}
% The proof proceeds by deriving two contradictory bounds on $\text{dist}(p(t'), p(t_0))$:

% \begin{enumerate}
% \item \textbf{Extracting the critical time:} The first \lstinline[language=lean]|obtain| extracts a time $t' \in [t_0, t_1]$ where $(p\ t')_2 = 1$. This gives us three components:
% \begin{itemize}
%   \item \lstinline[language=lean]|t' : unitInterval| ‚Äî the time value
%   \item \lstinline[language=lean]|ht' : t' ‚àà Set.Icc t‚ÇÄ t‚ÇÅ| ‚Äî proof that $t' \in [t_0, t_1]$
%   \item \lstinline[language=lean]|hpath_t' : (p t').2 = 1| ‚Äî proof that the $y$-coordinate is $1$
% \end{itemize}

% \item \textbf{Establishing the base point:} We prove \lstinline[language=lean]|hpt‚ÇÄ : p t‚ÇÄ = (0, 0)|, confirming that the path is at the origin at time $t_0$.

% \item \textbf{Showing proximity in time:} The statement \lstinline[language=lean]|t'_close| proves that $\text{dist}(t', t_0) < \delta$. The proof uses a \lstinline[language=lean]|calc| chain:
% \begin{itemize}
%   \item First, since $t' \in [t_0, t_1]$, we have $\text{dist}(t', t_0) \leq \text{dist}(t_1, t_0)$ (the distance from $t'$ to $t_0$ is at most the distance from $t_1$ to $t_0$)
%   \item By symmetry of distance, $\text{dist}(t_1, t_0) = \text{dist}(t_0, t_1)$
%   \item From our earlier work, $\text{dist}(t_0, t_1) < \delta$
% \end{itemize}

% \item \textbf{Upper bound from continuity:} The statement \lstinline[language=lean]|close| applies our earlier continuity result: since $\text{dist}(t', t_0) < \delta$, we have $\text{dist}(p(t'), p(t_0)) < 1/2$.

% \item \textbf{Lower bound from geometry:} The statement \lstinline[language=lean]|far| proves that $1 \leq \text{dist}(p(t'), p(t_0))$. The \lstinline[language=lean]|calc| chain shows:
% \begin{itemize}
%   \item $1 = |(p\ t')_2 - (p\ t_0)_2|$ by substituting $(p\ t')_2 = 1$ and $(p\ t_0)_2 = 0$
%   \item $|(p\ t')_2 - (p\ t_0)_2| \leq \|p(t') - p(t_0)\|$ by the fact that the norm dominates the absolute value of any component (\lstinline[language=lean]|norm_ge_abs_snd|)
%   \item $\|p(t') - p(t_0)\| = \text{dist}(p(t'), p(t_0))$ by the definition of distance in a normed space
% \end{itemize}
% This completes the proof by contradiction, showing that $T$ is not path-connected.
% \section{$T$ is connected not path-connected}
\section{Wrapping up}
Finally, we combine the two parts in the following concise and pleasant theorem:
\begin{lstlisting}[language=lean]
theorem T_is_conn_not_pathconn : IsConnected T ‚àß ¬¨IsPathConnected T :=
  ‚ü®T_is_conn, T_is_not_path_conn‚ü©
\end{lstlisting}
And now, since this code compiles successfully, these two lines stand as verified witnesses
to the correctness of our entire proof.
This showcases the power of proof assistants and formal reasoning.
Mathematics becomes not only more rigorous but also automatically verifiable.
Furthermore, the formalization becomes a learning tool in its own right.
Future readers can inspect each part of the code.
Here the full proof: [\href{https://live.lean-lang.org/#codez=JYWwDg9gTgLgBAWQIYwBYBtgCMBQEwCmAdnAEoFLpwDKB8AYsOjAVHACr4ToQDmAnjhwATAgDM4kAM4B9KBSoAuALw06AOgCSEYHAAUABjiK4gXEIAlDiIQiAYwjgArjCRZ0BOKIlTgRAjNtHKAA3DxU4MUcSAA84QDLCfWiAGjIFdR8SPWjAbwIATvNLLxpjNXg9UzgAdbNzY1U9DP9AkIJagHI2yQhZeUoRcTgALRLaMorqizq4AG99AxSDWoBffol2Ebp9cZqp6jhAKiIhoQBaY444YCk4eyI/WxZhHHcQECQOGSlHLAD0WT2TdaAMKJrjxPvJiuEsII4BciDAoBA4LFUNEcDDbEgpAQrii4AB3YBoNFwAA+sKoKL2ygAfHACNEkPc4J8sFiYD9ukEPJTiWTfGwUcMacSYTCoHi4ABtQUAXRFovc8AkJkAqIRwQBJhGYqlrwpESCQafo9CQAFSpSjqMDAcx5BaWUWi1BIUJwVDKjjEYRSGCIiQoThgfSAVg3ALs7czttUh0IdovkYl8HktCOECAA1jIiKgvXAAPwyOAsIhen0BGzejNZqTyh30xnwQvFiAyXzBGT+/AyABerAg6ns4GrMb0jGYrHU7E93t77bAMhAjnQpaI3s65AtVpk0gLk5LRBQAGFMeyZ22YAH7TGnS63Z0TIAAIkABbhwEi+OAzlISEiACCIIaooYOYXjUcoBkRwwDxJAoGzSUCFCOFHEodB+BkXhjzPfA4AMGV8UJVBn1dIgALpBkmUAC/JjTgM013QS1rVtIiYRbTdunUcA2D0edFy3PQADkUD7I9mKkViwAFIhamo2ihPMJIGLgKFmVAQNJTEFJGgCIJQhSJiWxSSSMgzFA5wXTdgBlQBL8kHWsmRAAgQA5MF/AgMQZAbKdXQkG8cDQAhoDs95LiXEgTE0KR9xsO4HnOFRiVC8LbgIe4CGEdIvjZByuX0S54si5KZG0YB1FAJBeA8dTmhdAAeEl5RuGBfEcCBHFkYAUrqhqmqkAB5IgkwgFNUzgar5Uk9qiEa5qPl8PsHFnMaJu6khhodeL6vGzqeqKohgkAAIJrhsNaFubNqDo65qevlPQ9TgfNcUNPwZGclD4BRAp9AUlxnCbNlanYD4vh+P4TjOdZLjgHiIHgAAFFBUGOG5cseU44GcJhCX4OBnleJ47NeZ9oHs0q21ZD4ixmN4sBKUwlhKQA0Aj0JAUiwcxabgQBTIlJLAySjRSB2RrwEzfLofHql0sQAR0cYhbA8V89k+Ww8LQFB8VQYh9vAbpkpwtACzVxSiH3TTZauSgIP4K4AEYrBsfswGcVx3E8AZolkaRNwoQbdSifQvbgFVakNAAmAB6PQ9AAFkouBBoAajgS3aiotIrUsLG3ldoSPaQdM3JLbsERKCci3czP3cIHO33QwM9FDTDIz/aM40F4vGxm8B9F4KZrr9ioEiDuAQ6NKOzTjhOk/NGjU4vGE823EvdwPQSTxnYkfA7mwkKlYRgFbAhxeMxddOZVAIAlOujFUOu9AHs1DCpt6FPXsA5Rhaz4FbqcZuXdlOLgAfqoegXr2O289GzNm2qeAMXYexDRqg6T+Pp1Ank4kub0k8pJbkAXnJsK9hDCDQeyKAwBeCoHgPmS2cD5SIJAWWX+JkZwRCaiBLcODjpCHTkiN23Rs7pmsAlLu94Y4lBVAsOAgATIi4VnCug0vaqB7nAYUMId6tn4X4LuegFLWCgPZcaIBaiaIxtIQku90Zp1xm8AyZceEyJkPvR6fhfYiMjBgjIRprFu09jHG0+RFEJymP+GEV4wiJG4Z4yuqYfF+MklaQe/84Dx0GnfW+GDU4BOjDCZ+UoPG8JlAAbgiMAAg6ACHPwKcQogvA15KSlGgS4ek0gGSQPgwy9DFwwDxE2K0DSLQGQ3Co1ynS5SrDgJ2KmlQKjhEMHaG2dhZoOzcB4IoEoTCmAmVMcqxt9CJyED5PyIAAqyGsOyMAsMgolAADX6FCjDNAOVEpRXYLUdJxJkYAEEpCfFsvibkzoPBvFObrMQCIDmdnqPMeuBZER4mUHoS2alfBwoKMSYJroxkmDGT+dY4QupQC2mwKAYh0Aor+a6FZ+I4BYqmLiraVBSLwrgKRAuTZmAOIIJ0UwKRCXoHMpZRicJC5zQisSJAYAwBbxubDe5SUUoACsdB+GEJoEgKshW3DXoQWwwBKDAG7JIc5YzUBjIlKgPExJFSSCmAAKQVclZV6QHAEFuXhNVhEYTIxuD4UQbAfIa2MfVGwcBnJ6w8NEew0Ad57hYHAV4gYCS6zACkAkitGJXHmp1ElLow0QAjZuWGdUSirTOlca68AEh6EDDAcw6hKHhHTZNMQ3p26BjAN/Q6Gb3VnAtTAPavr0BHgLKADwvrAV4TBoG31/BjhIGiJcc1mwe0lCiISZVLAQiUA2dQMCMx4BkgrQWatlDVAGBWDCC12bc2ju7j7eAhp91Vpra8s4vqL1QV8CgDwwar0q0XWDAwmbuRgB7TIWIJh707UPX4owMUHR6GyqCPKGReCKhsEmAgxVSpInDVBPNaA6rVqkFu2ctkDmkQhQpJA2J8B8rgMjPQABmdQQdagWsAK3AfjLYhyDkzDG810ZBokCO2GSaPAYhIPGMmbxAAtwHAWkRgFZKydDAJ9TC2CIQLLCWpO0UiLvjlJmUKQSQkjAHoKttHJCmYg0ZoaCcuMqbVnYYd+sr1g1fBTdTwaoDNOAM1WzA9IJNTJtM+uxIIBYBcK+UiMmUioCkzFmA5kSiAGAiOAMm5MpDvAWFIO90H1j2pVVLGp5Q5bKJWgxlaIM2c4wPGDooEB0GIbYdQsFiAwAQugJCFYvTNjEGIEScDEANeAE1thmYvT9YrW24tby8u1HzDYfwTp0AuWkCp7tgBAgnkk5/5g7vlCd1mDY5QaSCTunbOqsnaCybdc5kHtOm9p6dfrRs4DmZZXdkwWHaoXwtIEizAdb8X1uJZMCl/7OnNu0kXXAQA5ESeEuHl97BWZNRnlBaqQe0pmLtWXAWoHqB2LuBQ4FGRAV1wlYMEDdPotRiGgDGpAJP7b9oDW6hUmwpCbfCCAV8DQHupa47Ua2DpUWoHRzIUQzAkBCRKEYSR6OEl85q6oZpBC1EEC7j2pjNb3pGO6CY4IZj5TC/Z9LiRzIOeqHcHOV8Iudpi+KS4KXzKZCW4W4b0lIv1ttBMMbyRtbVBc6IM7/wxDSHwHUOoeUzUPCkXZzF2Prp2dtBo+iOhUBHD3GgPKAA7bzQMm8MaSlQj1lyzA1IA/R0kQACYQ0C+DAfghAZr+FZfYAgT2HSivFRjVlAfBw5/fvoVlyvi9ZxD2Qm61a2L6CWyt7oropMz1FDnjvW9WVPRdy5Rxehl0wFXeTi0LvHE9oXw6HPvh4SIjIV9mMl5SV4hA7hvCYHAyJy16oP38kMZZMlJsloSa2/X9dFJWA3sVrU+w412Fr3rwIGa2iBeh7TklRWiDsQPmZRKH3RfyPUwheQAJjHFClGr2AIPktn/xwP71QCA1t1RBwLPXfWISJBP0AOvCdm9021l15yky4ymADyDyDzEHIRukHGF0w29z2ll3N0xn8AD210xloNwlqA1yDiY0HDwMlC3XCygPUBK2QOy3h1LBeB0PLH3hSFcFkCelVw0QUkwD3DoNQHMBIJkOsNwifS7U2DeFUFfQISvX+w+3/UuzqSuHnHQRpw6zPg/2dgkGDUXXp2EBhEXQK3+xUykERG8MCPgCwFlhID2GTTwidCuH9V3lDX2gjXfRYHnXgDeCmUrXW0PQA0AJKDeDk2wNFDCwixIFImSFdGiE0BiyQP3mB0tW8J/H+EbkHAxCxDyKCgdVsmdTnH8m8PjVQEHD5CIApCpFpDIOoCWNhAFCFGpEHGRk0AkCqMpSGB0wc30CqMg2PSIlRSfyuyuKwJ5hUNQEGHsMu2oGSNYM+xSDqi8x3nuF8HVwh0+xuPd2YPe0kSxwt38EI23X8NBKYODwUDQO8NWVqEkUswfggPUIbxbx4JbxEgFHcARO5CL16CUH0FRJ2FpExLROxLr1xIgCb3ZAJMnzIXW0fRwKsMgicIdGfjeHzylEZjgGrxRGQOwmakBM6M0BU3YH1gw1DQrT0ElDuyuxlDejBgRgeW1miP2jhF+2XCwN1KQFlLVjjD8ilAhSQGwmBGiCVJVO0zVORSCVJU0FsFsEehckl2+BZHyndNclt3+xMFGC0HdKNLgFtOw08NhjgA6FKFDNsDANROUGrDigim1JSjdI9JZDZEHHDmynTJlT9MTL0HX2dzgI5MTmrQVJuiwxzRw1HTqim3WnOnEjzNIlVPcD4NmPsizMJIHychLwrJrRkldAoJA3MmP30A7IBzgFH1/jsmLP7NLMHPLNdH+xHK5SJUnLW02ByRkSFglilkcwEyRGOAp3QCliuDxDNLKkRU42iEDgThU0KmgNUzfCIAxkGh/DVARSIDhRDn3M9j0EiSfMF2eykXLi8RuFCCgFKiuCpyMExGEU1EADwiFTNAXzHycESCMqR1WEf4opK4XQGnNgbIyCmxLxXQG7S04wp7RA92XI1yVgEAFqQPLMz0tsGQSmEHC4ERbSdmfxWHICyuXQIYjQLM8MlHB0Fog0xlHiGLHiYHeoereEYbdQUbSsfrES3OHcJsVAhmfnPMhSfI/XOvAoGA+HWQUixCTi1eB0KPOnWIShHieUZuRxUiS3V4JArs/gmQFIHSjMdMrufy3MGQZPRgkJErUJaRLxDiGdfxHiN6IwArN4IeRXV0HiWslcucGdOQEhMffMGQBfFQySLQow5kAGZlYwkmMw4KmKqCnOIKgRcfbCH9VAOdB0bkmwlTCGCUPEUTenE+UI5WesW84ieHXbYdToV8B08HG0rTe7eXefSqlNUaizGANoJ8vQE0JIHZGEWSv7NoeLY6scs5Ta/o0HTocSmABMpM2c+9LapjcA6S5on7SLYAGLEhAgL65SpERizEZinRNi4suy7it3F0EwHSvin8Dwh/WMzoEMySxdZMuADip6b0/6b4Di4Dbwjq+UQ6tozak6nskm6IP6+EhyrEAsU6shNoHs+UZGagU+CUSta64oY0SGTTQYP8t7dazpBcGI14VMUNMJXhPi49BfYXXwGQf4AY9m4Yj/UYzEbEfVBGKYp1M5EjGmnWRY6/ZY1YxRdYkiF6TY/W7Y10XYkk10exVAkwNmvxYLRYXYDQEjKaSpFDQPYAXrfrF462qGsWg83QY9JonAjEdARM6G4AOSB0eoNmyDHmfk7JKMh/CUpDdc+m6IdIfgF4GO2s8FO0LXJ4iUaUW2nsN4mMfMEO8IblIiJOlUjqqQNOqUqxQOz2ZAtlC4Rms4agXwN7B2oYkTSK+Wx2jojIACx82oUi5kfC2IOTAm96tojo0KtWcWP6jqwPM20UZGAAIWcCRD8Sjv0EJwORfXhnrMjQ/VHKSINgnrAohu5FAwPvcLbtEtDsdFJTuM2oeNA0blzzeG/wTA0haGbsqT1N4CgDeTgrgChmTHUEbRen3mztzuv3DsjsdvjqLtUH8OQZADkirrrMvRjM2qmFruoPzoovCUGl0HCDppAzcpLtFIqp/y0k6P/371btiqavsQWy7s7QABFWACiQ1Ck9wqBfjvMATA1JSwHiaM6YtR1XI2gVNGAoB0EpAWa9ZVUwCC6QsXTrwKCSgKtHaIVnbXr0QVaJj1akjpita5i9oFj5QDbXQ1jBxCbGUOixTpAejkD16t6hdP7n7HiRiw7KB0H6gWHQ0E7VAyHyGKHwNomwj67InQGu4bgIGoGu5YH+p4H0ExT9486CHoNVByDgMqCYwv93ZyS2qXokDVtr9ureTRRlidijbiI6x4y3akNPbi9fbBgMKZrrzcJXwZNIi9oGg+7pqTi5q1SPyYjoqUbNskcLxUVNqHIorJqSG4jCtzHrgwm4dctOh4CcDJEFnNsoSDmiECr2Qyy3bHBJK9AOKcy6BQIszOg6apyCGFne1xCtD+wDkirBx8wkcph2SlDLsd7eNTpxp0YUgTNv6Lg00EMoUNqINajbBkWTBorHrytPtagCtqtQX6wjn6aMWtYVM974B4WtrACrgp0oySiPAGUb7CQ2gS1IIIgQVUXtr5hFhaixAOWTBKFTnJqcWLi8X360H/FVASQcXnqzgEmmNuY/6v9yDzraaKCK78xJFaYHazgKtWZwhtFCZ/ATDSZHgcCvnRX47xWj90k5yS7yqD5jWntGmiRvI1Z9lDkgogqTl80IoSg0yEoZVzhYcLlJU7lCzHk6hiRSI/pAoEYkh42jlIYH9zlLIgA}{link to Lean live}]

